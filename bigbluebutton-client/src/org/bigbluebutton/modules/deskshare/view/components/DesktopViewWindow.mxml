<?xml version="1.0" encoding="utf-8"?>

<!--

BigBlueButton open source conferencing system - http://www.bigbluebutton.org/

Copyright (c) 2012 BigBlueButton Inc. and by respective authors (see below).

This program is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License as published by the Free Software
Foundation; either version 3.0 of the License, or (at your option) any later
version.

BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License along
with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.

-->

<mx:Canvas xmlns="org.bigbluebutton.common.*"
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:common="org.bigbluebutton.common.*"
    width="600" height="400"
	initialize="init()"
	creationComplete="onCreationComplete()"
	implements="org.bigbluebutton.common.IBbbCanvas"
	xmlns:mate="http://mate.asfusion.com/"
	backgroundColor="#C0C0C0">
	
	<mate:Listener type="{CursorEvent.UPDATE_CURSOR_LOC_EVENT}" method="onUpdateCursorEvent" />
	<mate:Listener type="{LocaleChangeEvent.LOCALE_CHANGED}" method="localeChanged" />
		
	<mx:Script>
		<![CDATA[
			import com.asfusion.mate.events.Dispatcher;
			
			import mx.core.UIComponent;
			import mx.events.ResizeEvent;
			
			import flexlib.mdi.events.MDIWindowEvent;
			
			import org.as3commons.logging.api.ILogger;
			import org.as3commons.logging.api.getClassLogger;
			import org.bigbluebutton.common.IBbbModuleWindow;
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.common.events.LocaleChangeEvent;
			import org.bigbluebutton.main.views.MainCanvas;
			import org.bigbluebutton.modules.deskshare.events.CursorEvent;
			import org.bigbluebutton.modules.deskshare.events.StartedViewingEvent;
			import org.bigbluebutton.modules.deskshare.events.ViewWindowEvent;
			import org.bigbluebutton.modules.deskshare.model.DeskshareOptions;
			import org.bigbluebutton.util.i18n.ResourceUtil;

			private var screenHeight:Number = Capabilities.screenResolutionY;
			private var screenWidth:Number = Capabilities.screenResolutionX;
						
			private var images:Images = new Images();
			[Bindable] public var fitToWidthIcon:Class = images.magnifier;
			[Bindable] public var fitToActualSizeIcon:Class = images.mag_reset;
			
			private var cursor:Shape;
			
			private var video:Video;
			private var ns:NetStream;
			private var videoHolder:UIComponent = new UIComponent();
			private var stream:String;
			private var videoHeight:Number;
			private var videoWidth:Number;
			
			private static const LOGGER:ILogger = getClassLogger(DesktopViewWindow);      

			private static const VIDEO_WIDTH_PADDING:int = 7;
			private static const VIDEO_HEIGHT_PADDING:int = 65;

			// The following code block is to deal with a bug in FLexLib 
			// with MDI windows not responding well to being maximized
			private var savedWindowWidth:Number;
			private var savedWindowHeight:Number;
			private var savedX:Number;
			private var savedY:Number;
			private var isMaximized:Boolean = false;
			
			[Bindable] private var dsOptions:DeskshareOptions;

			private var whiteboardCanvas:Canvas = null;
			private var whiteboardCanvasHolder:Canvas = new Canvas();
			private var fullScreen:Boolean = false;

			private var fullScreenWidth:Number = 1;
			private var fullScreenHeight:Number = 1;
			private var windowedWidth:Number = 1;
			private var windowedHeight:Number = 1;
									
			private function init():void{
				dsOptions = new DeskshareOptions();
			}
									
			private function onCreationComplete():void{
				videoHolder.addChild(video);				
				videoHolder.addEventListener(ResizeEvent.RESIZE, onResize);
				this.addChildAt(videoHolder,0);
				videoHolder.percentWidth = 100;
				videoHolder.percentHeight = 100;
				fitVideoToWindow();
				cursor = new Shape();
				cursor.graphics.lineStyle(6, 0xFF0000, 0.6);
				cursor.graphics.drawCircle(0,0,3);
				videoHolder.addChild(cursor);
				videoHolder.addChild(cursorImg);
				cursor.visible = false;
				
				resourcesChanged();
			}
			
			private function onResize(e:ResizeEvent):void {
				determineHowToDisplayVideo(fullScreen);
			}
			
			private function onUpdateCursorEvent(event:CursorEvent):void {	
				if(cursor == null)
				   return;

				cursor.x = ((event.x/videoWidth)) * videoHolder.width;
				cursor.y = ((event.y/videoHeight)) * videoHolder.height;
			
				cursorImg.visible = true;
                // DO NOT compute the x and y coordinate and assign directly to the cursorImg
                // as it results in a flickering and jerky mouse pointer (ralam jun 10, 2010).
                if (cursor.x < videoHolder.x) cursor.x = videoHolder.x;
                if (cursor.y < videoHolder.y) cursor.y = videoHolder.y;
                if (cursor.x > videoHolder.x + videoHolder.width) cursor.x = videoHolder.x + videoHolder.width;
                if (cursor.y > videoHolder.y + videoHolder.height) cursor.y = videoHolder.y + videoHolder.height;
                cursorImg.x = cursor.x;
                cursorImg.y = cursor.y;
			}
			
			public function startVideo(connection:NetConnection, stream:String, width:Number, height:Number):void{
				this.videoWidth = width;
				this.videoHeight = height;
				
				ns = new NetStream(connection);
				ns.addEventListener( NetStatusEvent.NET_STATUS, onNetStatus );
				ns.addEventListener(AsyncErrorEvent.ASYNC_ERROR, onAsyncError);
				ns.client = this;
				ns.bufferTime = 0;
				ns.receiveVideo(true);
				ns.receiveAudio(false);
				
				video = new Video(width, height);
				video.width = width;
				video.height = height;
        		video.smoothing = true;
				video.attachNetStream(ns);
				ns.play(stream);	
				this.stream = stream;

			}

			private function addWhiteboardCanvasHolder():void{
				if(video != null) {
					cleanCanvasHolder();
					updateWhiteboardCanvasHolder();
					this.addChildAt(whiteboardCanvasHolder,1);
					LOGGER.debug("Whiteboard canvas holder added");
				}
			}

			private function cleanCanvasHolder():void{
				while (this.whiteboardCanvasHolder.rawChildren.numChildren > 0)
				  this.whiteboardCanvasHolder.rawChildren.removeChildAt(0);
			}

			private function updateWhiteboardCanvasHolder():void{
				if(video != null) {
					resizeCanvasChildren();
					whiteboardCanvasHolder.x = video.x;
					whiteboardCanvasHolder.y = video.y;
					whiteboardCanvasHolder.width = video.width;
					whiteboardCanvasHolder.height = video.height;
					LOGGER.debug("Whiteboard canvas holder dimensions updated");
				}
			}

			private function resizeCanvasChildren():void{
				var widthResizeRatio:Number = 1;
				var heightResizeRatio:Number = 1;

				if(fullScreen && whiteboardCanvasHolder.width == windowedWidth) {
				   widthResizeRatio = fullScreenWidth/windowedWidth;
				   heightResizeRatio = fullScreenHeight/windowedHeight;
				}
				else if(!fullScreen && whiteboardCanvasHolder.width == fullScreenWidth){
				   widthResizeRatio = windowedWidth/fullScreenWidth;
				   heightResizeRatio = windowedHeight/fullScreenHeight;
				}

				for (var i:int = 0 ; i < this.whiteboardCanvasHolder.rawChildren.numChildren; i++) {
					this.whiteboardCanvasHolder.rawChildren.getChildAt(i).width =
					this.whiteboardCanvasHolder.rawChildren.getChildAt(i).width * widthResizeRatio;

					this.whiteboardCanvasHolder.rawChildren.getChildAt(i).height =
					this.whiteboardCanvasHolder.rawChildren.getChildAt(i).height * heightResizeRatio;

					this.whiteboardCanvasHolder.rawChildren.getChildAt(i).x =
					this.whiteboardCanvasHolder.rawChildren.getChildAt(i).x * widthResizeRatio;

					this.whiteboardCanvasHolder.rawChildren.getChildAt(i).y =
					this.whiteboardCanvasHolder.rawChildren.getChildAt(i).y * heightResizeRatio;
				}
			}

			public function addWhiteboardCanvasOverlay():void {
				updateWhiteboardCanvasOverlay();
				if(video != null && this.whiteboardCanvas != null) {
					this.addChild(this.whiteboardCanvas);
					LOGGER.debug("Whiteboard Canvas OVERLAY added.");
				}
				else
				     LOGGER.debug("COULD NOT add whiteboard overlay");
			}

			private function updateWhiteboardCanvasOverlay():void{
				if(video != null && this.whiteboardCanvas != null) {
					this.whiteboardCanvas.x = whiteboardCanvasHolder.x;
					this.whiteboardCanvas.y = whiteboardCanvasHolder.y;
					this.whiteboardCanvas.width = whiteboardCanvasHolder.width;
					this.whiteboardCanvas.height = whiteboardCanvasHolder.height;
					LOGGER.debug("Whiteboard canvas overlay dimensions updated");
				}
			}
			
			public function stopViewing():void {
				if(ns != null) {
				   ns.close();
				   ns = null;
				}
				closeWindow();				
			}
						
			private function onAsyncError(e:AsyncErrorEvent):void{
				LOGGER.debug("VIdeoWindow::asyncerror {0}", [e.toString()]);
			}
			
			private function onNetStatus(e:NetStatusEvent):void{
				LOGGER.debug("onNetStatus info={0}", [e.info.text]);
			
				switch(e.info.code){
				case "NetStream.Play.Start":
					LOGGER.debug("NetStream.Publish.Start for broadcast stream {0}", [stream]);
					LOGGER.debug("Dispatching start viewing event");
					var dispatcher: Dispatcher  = new Dispatcher();
        			var viewEvent:StartedViewingEvent = new StartedViewingEvent(StartedViewingEvent.STARTED_VIEWING_EVENT);
        			viewEvent.stream = stream;
					dispatcher.dispatchEvent(viewEvent);
					break;
				case "NetStream.Play.UnpublishNotify":
					LOGGER.debug("NetStream.Play.UnpublishNotify for broadcast stream {0}", [stream]);
					stopViewing();
					break;
				}
			}
			
			public function getPrefferedPosition():String{
				return MainCanvas.DESKTOP_SHARING_VIEW;
			}
												
			private function fitVideoToWindow():void {
				videoHolder.percentWidth = 100;
				videoHolder.percentHeight = 100;
				
				const videoAspectRatio:Number = videoWidth / videoHeight;
				const windowAspectRatio:Number = videoHolder.width / videoHolder.height;
				if (videoAspectRatio > windowAspectRatio) {
					video.width = videoHolder.width;
					video.height = Math.floor(video.width / videoAspectRatio);
					video.x = 0;
					video.y = (videoHolder.height - video.height) / 2;
				} else {
					video.height = videoHolder.height;
					video.width = Math.floor(video.height * videoAspectRatio);
					video.y = 0;
					video.x = (videoHolder.width - video.width) / 2;
				}

				fullScreen = false;
				windowedWidth = video.width;
				windowedHeight = video.height;
			}
						
			/**
			 * resizes the desktop sharing video to actual video resolution
			 */
			private function fitToActualSize():void{
				videoHolder.width = Math.max(this.width, videoWidth);
				videoHolder.height = Math.max(this.height, videoHeight);

				video.width = videoWidth;
				video.height = videoHeight;
				video.x = Math.max(videoHolder.width - videoWidth, 0);
				video.y = Math.max(videoHolder.height - videoHeight, 0);

				fullScreen = true;
				fullScreenWidth = video.width;
				fullScreenHeight = video.height;
			}
			
			public function determineHowToDisplayVideo(actualSize:Boolean):void {
				if (actualSize)
					fitToActualSize();			
				else
					fitVideoToWindow();

				//update the whiteboard canvas holder and overlay with new video dimensions...
				updateWhiteboardCanvasHolder();
				updateWhiteboardCanvasOverlay();
			}
			
			private function closeWindow():void {
				dispatchEvent(new ViewWindowEvent(ViewWindowEvent.CLOSE));
			}
			
			override protected function resourcesChanged():void{
				super.resourcesChanged();
			}
			
			private function localeChanged(e:Event):void{
				resourcesChanged();
			}

			/** Inherited from IBbbCanvas*/
			public function addRawChild(child:DisplayObject):void{
			       if(fullScreen) {
			          child.width = child.width * (fullScreenWidth/windowedWidth);
			          child.x =  child.x * (fullScreenWidth/windowedWidth);
			          child.height = child.height * (fullScreenHeight/windowedHeight);
			          child.y = child.y * (fullScreenHeight/windowedHeight);
			       }
			       this.whiteboardCanvasHolder.rawChildren.addChild(child);
			}

			public function removeRawChild(child:DisplayObject):void{
			       this.whiteboardCanvasHolder.rawChildren.removeChild(child);
			}

			public function doesContain(child:DisplayObject):Boolean{
			       return this.whiteboardCanvasHolder.rawChildren.contains(child);
			}

			public function acceptOverlayCanvas(overlay:IBbbCanvas):void{
			       LOGGER.debug("DesktopViewWindow: acceptOverlayCanvas");
			       whiteboardCanvas = overlay as Canvas;
			       addWhiteboardCanvasHolder();
			       addWhiteboardCanvasOverlay();
			}

			private function handleWhiteboardCanvasClick(e:MouseEvent):void {
			       LOGGER.debug("DesktopPublishWindow: handleWhiteboardCanvasClick");
			}

			public function moveCanvas(x:Number, y:Number):void{
			       LOGGER.debug("DesktopPublishWindow: moveCanvas");
			}

			public function zoomCanvas(width:Number, height:Number, zoom:Number):void{
			       LOGGER.debug("DesktopPublishWindow: zoomCanvas");
			}

			public function showCanvas(show:Boolean):void{
			       LOGGER.debug("DesktopPublishWindow: showCanvas");
			}
			
		]]>
	</mx:Script>

	<mx:Move id="cursorMove" target="{cursorImg}"/>
	<mx:Image id="cursorImg" visible="false" source="@Embed('../../assets/images/cursor4.png')"/>
</mx:Canvas>
