<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" click="setMessageRead()" xmlns:mate="http://mate.asfusion.com/" >
	
	<mx:Script>
		<![CDATA[		
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.Button;
			import mx.core.ScrollPolicy;
			
			public var read:Boolean = true;
			public var participantLoggedOut:Boolean = false;
			
			[Bindable]
			private var backgroundColor:uint = 0x000000;
			private var lastSenderName:String = "";
			private var lastTime:String = "";
			
			[Bindable] private var messages:ArrayCollection = new ArrayCollection();
			
			private function updateScroll():void{
				chatMessagesList.verticalScrollPosition = chatMessagesList.maxVerticalScrollPosition;
			}
			
			public function showNewMessage(message:String):void
			{	
				var date:Date = new Date();
				var timeString:String = getHours(date) + ":" + getMinutes(date);
				var components:Array = message.split("|");
				
				var msg:Object = new Object();
				msg.lastSenderName = lastSenderName;
				lastSenderName = components[1];
				
				
				msg.text = components[0];
				msg.senderText = components[0];
				
				msg.name = components[1];
				msg.color = uint(components[2]);
				
				msg.senderTime = components[3];			
				msg.time = timeString;
				msg.lastTime = lastTime;
				lastTime = timeString;
				
				messages.addItem(msg);
				
				chatMessagesList.invalidateSize();
				chatMessagesList.invalidateDisplayList();
			}
			
			public function showMessage(message:String, color:uint, sender:String, time:Number):void{
				var date:Date = new Date();
				date.setTime(time);
				var timeString:String = getHours(date) + ":" + getMinutes(date);
				
				var msg:Object = new Object();
				msg.lastSenderName = lastSenderName;
				lastSenderName = sender;
				
				msg.text = message;
				msg.senderText = message;
				
				msg.name = sender;
				msg.color = color;
				
				msg.senderTime = timeString;
				msg.time = timeString;
				msg.lastTime = timeString;
				lastTime = timeString;
				
				messages.addItem(msg);
				
				chatMessagesList.invalidateSize();
				chatMessagesList.invalidateDisplayList();
				
			}
			
			public static function getMinutes(time:Date):String{
				var minutes:String;
				if (time.minutes < 10) minutes = "0" + time.minutes;
				else minutes = "" + time.minutes;
				return minutes;
			}
			
			public static function getHours(time:Date):String{
				var hours:String;
				if (time.hours < 10) hours = "0" + time.hours;
				else hours = "" + time.hours;
				return hours
			}
			
			public function setMessageUnread():void{
				this.read = false;
				backgroundColor = 0xFFFF00;
			}
			
			public function setMessageRead():void{
				this.read = true;
				backgroundColor = 0x000000;
			}
			
			private function onLinkClick(e:TextEvent):void{
				var url:URLRequest = new URLRequest(e.text);
				navigateToURL(url, '_blank');
			}
			
			
			
		]]>
	</mx:Script>
	
	<mx:List width="100%" height="100%" id="chatMessagesList" updateComplete="updateScroll()" selectable="false" variableRowHeight="true"
			 itemRenderer="org.bigbluebutton.chat.views.ChatMessageRenderer" dataProvider="{messages}" />
</mx:VBox>